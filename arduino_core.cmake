function(include_arduino_core sketch_target core_path)
    if (NOT EXISTS ${core_path})
        message(FATAL_ERROR "Cannot Include Arduino Core - Path: ${core_path} does not exist")
    endif()
    if (NOT EXISTS ${core_path}/CMakeLists.txt)
        message(FATAL_ERROR "Cannot Include Arduino Core - Path: ${core_path} does not contain CMakeLists.txt")
    endif()
    if (NOT DEFINED ARDUINO_VARIANT)
        set(ARDUINO_VARIANT "standard")
        message(VERBOSE "Using default board variant")
    else()
        message(VERBOSE "Using Board Variant: ${ARDUINO_VARIANT}")
    endif()

    message(STATUS "Adding Arduino Core - Path: ${core_path}")
    add_subdirectory(${core_path})

    if (TARGET arduino_AvrCore)
        if (TARGET arduino_CompileFlags)
            target_link_libraries(arduino_AvrCore PUBLIC arduino_CompileFlags)
            target_compile_features(arduino_AvrCore PUBLIC cxx_std_11 c_std_11)
        else()
            create_compile_flags_library()
            target_link_libraries(arduino_AvrCore PUBLIC arduino_CompileFlags)
            target_compile_features(arduino_AvrCore PUBLIC cxx_std_11 c_std_11)
        endif()
        target_include_directories(arduino_AvrCore PUBLIC 
             ${core_path}/variants/${ARDUINO_VARIANT}
        )
        target_link_libraries(${sketch_target} PUBLIC arduino_AvrCore)
    else()
        message(FATAL_ERROR "Failed to add Arduino Core - Target arduino_AvrCore not present in ${core_path}/CMakeLists.txt")
    endif()

    message(STATUS "Successfully added arduino_AvrCore")
endfunction()